apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.brandonhogan.liftscout"
        minSdkVersion 21
        targetSdkVersion 25

        // Fetch the version according to git latest tag
        def longVersionName = "git -C ${rootDir} describe --tags --long".execute().text.trim()
        // No tag found
        if (longVersionName.isEmpty())
            throw new GradleException("\n--------" + "GRADLE ERROR--------"
                    + "\n"
                    + " - Could not find a valid tag associated to this build. Tag versioning " +
                    "must be in the Major.Minor.Patch format."
                    + "\n"
                    + "Example : 1.2.3")
        def branchName = "git -C ${rootDir} rev-parse --abbrev-ref HEAD".execute().text.trim()
        def (fullVersionTag, versionBuild, gitSha) = longVersionName.tokenize('-')
        def (versionMajor, versionMinor, versionPatch) = fullVersionTag.tokenize('.')
        if (versionMajor == null || versionMinor == null || versionPatch == null) {
            throw new GradleException("\n--------" + "GRADLE ERROR--------"
                    + "\n"
                    + " - Could not find a valid tag associated to this build. Tag versioning " +
                    "must be in the Major.Minor.Patch format."
                    + "\n"
                    + "Example : 1.2.3")
        }
        if (!versionMajor.isNumber())
            throw new GradleException("Tag version major is not numeric. " + versionMajor)
        if (!versionMinor.isNumber())
            throw new GradleException("Tag version minor is not numeric. " + versionMinor)
        if (!versionPatch.isNumber())
            throw new GradleException("Tag version patch is not numeric. " + versionPatch)
        // Set the version name
        versionName "$branchName $versionMajor.$versionMinor.$versionPatch($versionBuild)"

        // Turn the version name into a version code
        versionCode versionMajor.toInteger() * 10000 +
                versionMinor.toInteger() * 1000 +
                versionPatch.toInteger() * 100 +
                versionBuild.toInteger()

        // Friendly print the version output to the Gradle console
        printf("\n--------" + "VERSION DATA--------"
                + "\n"
                + "- Branch Name: " + branchName
                + "\n"
                + "- Version Code: " + versionCode
                + "\n"
                + "- Version Name: " + versionName
                + "\n"
                + "- Long Version Name: " + longVersionName
                + "\n----------------------------\n")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix '.debug'
        }
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.0.6'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    compile('com.mikepenz:fastadapter:1.6.0@aar') {
        transitive = true
    }

    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:support-v13:25.2.0'
    compile 'com.android.support:cardview-v7:25.2.0'
    compile 'javax.annotation:jsr250-api:1.0'


    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'org.greenrobot:eventbus:3.0.0'

    compile 'com.prolificinteractive:material-calendarview:1.2.0'
    compile 'com.github.sundeepk:compact-calendar-view:2.0.0'

    compile 'cn.pedant.sweetalert:library:1.3'
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.1'
    compile 'com.github.lawloretienne:discreteslider:0.0.9'
    compile 'com.jaredrummler:material-spinner:1.1.0'
    compile 'com.afollestad.material-dialogs:core:0.9.3.0'
    compile 'biz.kasual:materialnumberpicker:1.2.1'
    compile 'com.nikhilpanju.recyclerviewenhanced:recyclerviewenhanced:1.1.0'
    compile 'com.thebluealliance:spectrum:0.7.1'
    compile 'com.brandonhogan.accordionview:accordionview:0.1.1'
    compile 'com.mikepenz:fastadapter-extensions:1.7.0@aar'
    compile 'com.github.devlight:infinitecycleviewpager:1.0.2'
    compile 'agency.tango.android:material-intro-screen:0.0.5'

    compile 'com.google.firebase:firebase-core:10.0.1'
    compile 'com.google.firebase:firebase-crash:10.0.1'
    compile 'com.google.android.gms:play-services-tagmanager:10.0.1'
    compile 'com.google.firebase:firebase-messaging:10.0.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    compile 'com.github.ittianyu:BottomNavigationViewEx:1.1.3'

    debugCompile 'com.facebook.stetho:stetho:1.4.1'
    debugCompile 'com.uphyca:stetho_realm:2.0.0'
}

apply plugin: 'com.google.gms.google-services'