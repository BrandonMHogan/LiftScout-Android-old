apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'com.neenbedankt.android-apt'

android {
    signingConfigs {
        config {
            keyAlias 'lskeystore'
            keyPassword 'Zero9900'
            storeFile file('/Users/bhogan/bh/lift-scout-android/lskeystore.jks')
            storePassword 'Zero9900'
        }
    }
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.brandonhogan.liftscout"
        minSdkVersion 21
        targetSdkVersion 24
        // Fetch the version according to git latest tag
        def longVersionName = "git -C ${rootDir} describe --tags --long".execute().text.trim()
        // No tag found
        if (longVersionName.isEmpty())
            throw new GradleException("\n--------" + "GRADLE ERROR--------"
                    + "\n"
                    + " - Could not find a valid tag associated to this build. Tag versioning " +
                    "must be in the Major.Minor.Patch format."
                    + "\n"
                    + "Example : 1.2.3")
        def branchName = "git -C ${rootDir} rev-parse --abbrev-ref HEAD".execute().text.trim()
        def (fullVersionTag, versionBuild, gitSha) = longVersionName.tokenize('-')
        def (versionMajor, versionMinor, versionPatch) = fullVersionTag.tokenize('.')
        if (versionMajor == null || versionMinor == null || versionPatch == null) {
            throw new GradleException("\n--------" + "GRADLE ERROR--------"
                    + "\n"
                    + " - Could not find a valid tag associated to this build. Tag versioning " +
                    "must be in the Major.Minor.Patch format."
                    + "\n"
                    + "Example : 1.2.3")
        }
        if (!versionMajor.isNumber())
            throw new GradleException("Tag version major is not numeric. " + versionMajor)
        if (!versionMinor.isNumber())
            throw new GradleException("Tag version minor is not numeric. " + versionMinor)
        if (!versionPatch.isNumber())
            throw new GradleException("Tag version patch is not numeric. " + versionPatch)
        // Set the version name
        versionName "$branchName $versionMajor.$versionMinor.$versionPatch($versionBuild)"

        // Turn the version name into a version code
        versionCode versionMajor.toInteger() *10000 +
                versionMinor.toInteger() *1000 +
                versionPatch.toInteger() *100 +
                versionBuild.toInteger()


        // Friendly print the version output to the Gradle console
        printf("\n--------" + "VERSION DATA--------"
                + "\n"
                + "- Branch Name: " + branchName
                + "\n"
                + "- Version Code: " + versionCode
                + "\n"
                + "- Version Name: " + versionName
                + "\n"
                + "- Long Version Name: " + longVersionName
                + "\n----------------------------\n")
        signingConfig signingConfigs.config
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            debuggable true
            signingConfig signingConfigs.debug
            minifyEnabled false
            applicationIdSuffix '.debug'
        }
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:design:24.1.1'
    compile 'com.android.support:support-v13:24.1.1'
    compile 'com.android.support:cardview-v7:24.1.1'

    // Leak Canary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    // Dagger 2
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    compile 'javax.annotation:jsr250-api:1.0'

    // Butterknife
    compile 'com.jakewharton:butterknife:7.0.1'
    // Calendar
    compile 'com.prolificinteractive:material-calendarview:1.2.0'
    // Fancy Alerts
    compile 'cn.pedant.sweetalert:library:1.3'
    // Today ViewPager Transforms
    compile 'com.ToxicBakery.viewpager.transforms:view-pager-transforms:1.2.32@aar'
    // Spinner
    compile 'com.jaredrummler:material-spinner:1.1.0'
    // Fab toolbar
    compile 'com.github.fafaldo:fab-toolbar:1.2.0'
    // Material Dialogs
    compile 'com.afollestad.material-dialogs:core:0.8.6.2'
    // Number Picker used in weight dialog
    compile 'biz.kasual:materialnumberpicker:1.2.1'
    // Submit Button
    compile 'com.github.dmytrodanylyk.circular-progress-button:library:1.1.3'
    // Recycler View
    compile 'com.nikhilpanju.recyclerviewenhanced:recyclerviewenhanced:1.1.0'
    // Color Pickers
    compile 'com.thebluealliance:spectrum:0.5.0'
    // Accordion Control
    compile 'com.brandonhogan.accordionview:accordionview:0.1.1'
    // Fast Recycler Adapter
    compile('com.mikepenz:fastadapter:1.6.0@aar') {
        transitive = true
    }
    compile 'com.mikepenz:fastadapter-extensions:1.7.0@aar'

    // Firebase
    compile 'com.google.firebase:firebase-core:9.4.0'
    compile 'com.google.firebase:firebase-crash:9.4.0'
    compile 'com.google.android.gms:play-services-tagmanager:9.4.0'
}

apply plugin: 'com.google.gms.google-services'